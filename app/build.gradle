plugins {
    alias(libs.plugins.android.application)
}

// Функция для получения количества коммитов
def getGitCommitCount() {
    try {
        def process = "git rev-list --count HEAD".execute()
        process.waitFor()
        return process.text.trim().toInteger()
    } catch (Exception e) {
        println("Error getting git commit count: ${e.message}")
        return 1 // Возвращаем 1 если git не доступен
    }
}

// Функция для генерации versionName
def getGitVersionName() {
    try {
        def tag = "git describe --tags --abbrev=0".execute().text.trim() // Последний тег
        def commits = "git rev-list --count ${tag}..HEAD".execute().text.trim().toInteger()
        return "${tag.replace("v", "")}-rc${commits}"
    } catch (Exception e) {
        println("Error getting git version: ${e.message}")
        return "1.0.0" // Версия по умолчанию
    }
}

android {
    namespace 'com.example.opencalendar'
    compileSdk 35

    defaultConfig {
        applicationId "com.example.opencalendar"
        versionCode getGitCommitCount()
        versionName getGitVersionName()
        minSdk 29
        targetSdk 34

        // Для аппаратного ускорения используйте manifestPlaceholders
        manifestPlaceholders = [androidHardwareAccelerated: "true"]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        setProperty("archivesBaseName",
                "war_policy-v${defaultConfig.versionName}-build${defaultConfig.versionCode}")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.databinding.adapters
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    // Room components
    implementation "androidx.room:room-runtime:2.6.1"
    annotationProcessor "androidx.room:room-compiler:2.6.1"

    // ViewPager2
    implementation 'androidx.viewpager2:viewpager2:1.1.0'

    // Lifecycle components (добавлено для лучшей работы с Room)
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.6.2"
}